buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.7"
    }
}

plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

group 'io.ankburov.gradle'
version settingsVersion

apply plugin: 'groovy'
apply plugin: "com.gradle.plugin-publish"

repositories {
    mavenCentral()
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()

    testCompile gradleTestKit()
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.196'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    testRuntime files(createClasspathManifest)
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}

pluginBundle {
    website = 'https://github.com/AnkBurov/database-update-plugin'
    vcsUrl = 'https://github.com/AnkBurov/database-update-plugin'
    description = 'This plugin incrementally updates any relational databases in simple and lightweight way'
    tags = ['database', 'update']

    plugins {
        dbupdateplugin {
            id = 'io.ankburov.gradle.dbupdateplugin'
            displayName = 'Simple alternative to Liquibase and Flyway projects for incrementing relational database updates'
        }
    }
}
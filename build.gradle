plugins {
    id 'net.saliman.cobertura' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.8.1'
}

group 'io.ankburov.gradle'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'

cobertura.coverageFormats = ['html', 'xml']
cobertura.coverageSourceDirs = sourceSets.main.groovy.srcDirs

repositories {
    mavenCentral()
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    compile gradleApi()
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'

    testCompile gradleTestKit()
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.8.47'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.196'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    testRuntime files(createClasspathManifest)
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
}